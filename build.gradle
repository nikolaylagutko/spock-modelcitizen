/**
 * Main configuration file
 */
buildscript {
	repositories {
		jcenter()
	}
	
	dependencies {
		classpath group: 'com.github.ben-manes',		name: 'gradle-versions-plugin',			version: gradleVersionsVersion
		classpath group: 'nl.javadude.gradle.plugins',	name: 'license-gradle-plugin',			version: gradleLicenceVersion
		classpath group: 'org.kt3k.gradle.plugin',		name: 'coveralls-gradle-plugin',		version: gradleCoverallsVersion
		classpath group: 'com.jfrog.bintray.gradle',	name: 'gradle-bintray-plugin',			version: gradleBintrayVersion
		classpath group: 'org.jfrog.buildinfo',			name: 'build-info-extractor-gradle',	version: gradleArtifactoryVersion
		classpath group: 'com.github.adrianbk',			name: 'gradle-travisci-trigger-plugin',	version: gradleTravisCIVersion
		classpath group: 'net.researchgate',			name: 'gradle-release',					version: gradleReleaseVersion
	}
}

apply plugin: 'java'
apply plugin: 'groovy'

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'net.researchgate.release'

apply from: 'gradle/custom/ide-java.gradle'
apply from: 'gradle/custom/licence.gradle'
apply from: 'gradle/custom/jacoco.gradle'
apply from: 'gradle/custom/publish.gradle'
apply from: 'gradle/custom/travisci.gradle'
apply from: 'gradle/custom/findbugs.gradle'
apply from: 'gradle/custom/codenarc.gradle'
apply from: 'gradle/custom/pmd.gradle'

repositories {
	jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	compile group: 'org.spockframework',			name: 'spock-core',	version: spockVersion
	compile group: 'com.tobedevoured.modelcitizen',	name: 'core',		version: modelcitizenVersion
	compile group: 'org.codehaus.groovy',			name: 'groovy-all',	version: groovyVersion
	
	testCompile group: 'junit',					name: 'junit',		version: junitVersion
	testCompile group: 'cglib',					name: 'cglib',		version: cglibVersion
	testCompile group: 'org.objenesis',			name: 'objenesis',	version: objenesisVersion
}

test {
	exclude 'org/gerzog/spock/modelcitizen/test/specs'	
}

//some hacking to compile Groovy first (since it contains Trait class that used in Java-based AST Transformation)
sourceSets {
	main {
		groovy {
			srcDirs = ['src/main/groovy', 'src/main/java']
		}
		java {
			srcDirs = []
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = gradleWrapperVersion
}